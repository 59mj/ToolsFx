plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.5.30"
    id 'application'
    id "com.diffplug.spotless" version "5.15.0"
    id "io.gitlab.arturbosch.detekt" version "1.18.1"
    id "org.openjfx.javafxplugin" version "0.0.10" apply false
}
def isJdk11 = isJdk11.toBoolean()
if (isJdk11) apply plugin: "org.openjfx.javafxplugin"
group = 'me.leon.toolfx'
version = '1.7.0'

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    maven { url 'https://maven.aliyun.com/repository/google' }
}

application {
    mainClassName = "me.leon.MainKt"
}

dependencies {
    implementation "no.tornado:tornadofx:$tornadofx_version"
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
    runtimeOnly "org.jetbrains.kotlin:kotlin-reflect:1.5.30"
    implementation 'org.glassfish:javax.json:1.1.4'
//    implementation 'com.jfoenix:jfoenix:8.0.10'
//    implementation 'com.github.bkenn:kfoenix:0.1.3'

    implementation 'com.google.zxing:javase:3.4.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.5.30'
}
if (isJdk11) {
    javafx {
        version = "18-ea+2"
        modules = [
                'javafx.controls',
                'javafx.swing',
//            if you use javafx.fxml,then uncomment it
//            'javafx.fxml'
        ]
    }
}


apply from: "$rootProject.projectDir/config/codeQuality.gradle"
def hook = new File("$rootProject.projectDir/.git/hooks/pre-commit")
hook.text = """#!/bin/bash
echo "run code format"
./gradlew spotlessJCh spotlessKCh
echo "run code smell check"
./gradlew detekt
"""